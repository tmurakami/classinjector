plugins {
    id 'com.github.ben-manes.versions' version '0.18.0'
    id 'pl.allegro.tech.build.axion-release' version '1.9.1'
}

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /:$v'/ },
                           replacement: { v, p -> ":$v'" }]
        pre 'commit', { v, p -> "Release version: $v [ci skip]" }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    version = scmVersion.version
    group = 'com.github.tmurakami.classinjector'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}

ext.deps = [jsr305 : 'com.google.code.findbugs:jsr305:3.0.2',
            junit  : 'junit:junit:4.12',
            mockito: [inline: 'org.mockito:mockito-inline:2.19.0',],
            android: [test: [rules: 'com.android.support.test:rules:1.0.2',],],]
