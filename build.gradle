plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply from: "$rootDir/gradle/version.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    group = 'com.github.tmurakami.classinjector'
    version = version_name
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}

ext.deps = [classinjector      : project(':classinjector'),
            jsr305             : 'com.google.code.findbugs:jsr305:3.0.2',
            junit              : 'junit:junit:4.12',
            mockito            : 'org.mockito:mockito-inline:2.9.0',
            rules              : 'com.android.support.test:rules:1.0.0',
            support_annotations: 'com.android.support:support-annotations:26.0.0',]
