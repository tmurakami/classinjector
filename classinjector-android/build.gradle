import com.android.ddmlib.DdmPreferences

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

apply from: "$rootDir/gradle/android.gradle"

android {
    defaultConfig {
        minSdkVersion 14
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    DdmPreferences.setTimeOut 60000
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    api deps.classinjector
    compileOnly deps.support_annotations
    testImplementation deps.junit
    testImplementation deps.mockito
    androidTestImplementation deps.rules
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
    from android.sourceSets.main.resources.sourceFiles
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
