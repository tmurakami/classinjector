import com.android.ddmlib.DdmPreferences

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

apply from: "$rootDir/gradle/android.gradle"
apply from: "$rootDir/gradle/install.gradle"

android {
    defaultConfig {
        minSdkVersion 14
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        consumerProguardFile 'proguard-rules.pro'
    }
    DdmPreferences.setTimeOut 60000
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    api project(':classinjector')
    compileOnly deps.jsr305
    testImplementation deps.junit
    testImplementation deps.mockito.inline
    androidTestImplementation deps.android.test.rules
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
    from android.sourceSets.main.resources.sourceFiles
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
