buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 9
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions.preDexLibraries !(System.getenv('CIRCLECI') as boolean)
}

dependencies {
    compile deps.classinjector
    provided deps.support_annotations
    testCompile deps.junit
    testCompile deps.mockito
    androidTestCompile deps.rules
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
    from android.sourceSets.main.resources.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += files(configurations.compile, configurations.provided)
    afterEvaluate {
        owner.classpath += files(android.bootClasspath)
    }
    options {
        def androidApi = 'https://developer.android.com/reference/'
        links androidApi
        def v = System.properties['java.version'] =~ /\d+/
        if (v.size() == 4 && ((v[0] as int) > 1 || (v[1] as int) > 8 || (v[2] as int) > 0 || (v[3] as int) >= 121)) {
            addBooleanOption '-allow-script-in-comments', true
        }
        bottom "<script>var l=document.links;for(var i in l){var e=l[i];if(e.href.indexOf(\"$androidApi\")===0)e.target=\"_blank\"}</script>"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
