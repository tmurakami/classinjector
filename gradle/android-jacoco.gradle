apply plugin: 'jacoco'

apply from: "$rootDir/gradle/android-common.gradle"

def jacocoReportTask = task jacocoTestReport()
androidVariants.all {
    def variantName = name
    def testTaskName = "test${variantName.capitalize()}UnitTest"
    def jacocoTaskName = "jacoco${testTaskName.capitalize()}Report"
    def jacocoTask = tasks.create(jacocoTaskName, JacocoReport) {
        reports {
            def reportDir = "$buildDir/reports/jacoco/$testTaskName/$variantName"
            html.enabled = true
            html.destination "$reportDir/html"
            xml.enabled = true
            xml.destination "$reportDir/${jacocoTaskName}.xml"
        }
        sourceDirectories = files(javaCompile.source)
        classDirectories = fileTree(
                dir: "$buildDir/intermediates/classes/debug",
                excludes: ['**/BuildConfig.class', '**/R.class', '**/R$*.class'])
        executionData = files("$buildDir/jacoco/${testTaskName}.exec")
    }
    jacocoReportTask.dependsOn jacocoTask
    jacocoTask.dependsOn testTaskName
    tasks[testTaskName].finalizedBy jacocoTask
}
