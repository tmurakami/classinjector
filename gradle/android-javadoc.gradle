apply from: "$rootDir/gradle/android-common.gradle"

task javadoc(type: Javadoc) {
    classpath += files(configurations.compile, configurations.provided)
    androidVariants.matching { it.name == 'release' }.all {
        owner.dependsOn tasks["prepare${name.capitalize()}Dependencies"]
        owner.source += javaCompile.source
        owner.classpath += files(android.bootClasspath, javaCompile.classpath)
        owner.classpath += files(configurations["${name}Compile"], configurations["${name}Provided"])
    }
    excludes = ['**/BuildConfig.java', '**/R.java']
    options.links 'https://developer.android.com/reference/'
    def version = System.properties['java.version'] =~ /\d+/
    if (version.size() == 4 && ((version[0] as int) > 1 || (version[1] as int) > 8 || (version[2] as int) > 0 || (version[3] as int) >= 121)) {
        options.addBooleanOption '-allow-script-in-comments', true
    }
    options.bottom '<script>var l=document.links;for(var i in l){var e=l[i];if(e.href.indexOf("https://developer.android.com/reference/")===0)e.target="_blank"}</script>'
}
